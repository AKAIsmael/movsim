# gnuplot command file

# parameterised startStop command file
# invoke using -e option to set ldm parameter, eg:
# gnuplot -e "lang='de'" onramp_IDM.gpl

if (!exists("lang")) lang = "en"
if (!exists("ldm")) ldm = "IDM"
#print "LDM=".ldm
#print "lang=".lang

# variables
baseFile = "onramp_".ldm
vehicle = ldm."1"

# input files
fundFile = baseFile.".fund_".vehicle
datFile = baseFile.".dat"
detFile(n) = sprintf(baseFile.".x%d_det",n)
roadSourceFile = "onramp_IDM.upBC_log"
rampSourceFile = "onramp_IDM.rmp1_log"

# output files
fundOutput = "figs/".baseFile.".fund_".vehicle.".eps"
veqOutput = "figs/".baseFile.".veq_".vehicle.".eps"
vqOutput = "figs/".baseFile.".vq_".vehicle.".eps"
detOutput = "figs/".baseFile.".det_q.eps"
detmicroOutput = "figs/".baseFile.".det_q_micro.eps"
inflowOutput = "figs/".baseFile.".inflow_log.eps"
v2dOutput = "figs/".baseFile.".v2d.eps"
v2dallOutput = "figs/".baseFile.".v2d_all.eps"

print "fundOutput =",fundOutput
print "please make sure that figs/ already exists"


# languages

# English
fundTitle =  "Fundamental diagram Q({/Symbol r})"
veqTitle = "Equilibrium velocity V_{eq}({/Symbol r})"
vqTitle = "V-Q equilibrium relation V_{eq}(Q)"
densityLabel = "Density {/Symbol r} (vehicles/km)"
flowLabel = "Flow Q (vehicles/h)"
veqLabel = "Velocity V_{eq} (km/h)"
timeLabel = "Time (s)"
timeMinuteLabel = "Time (min)"
distanceLabel = "Distance (m)"
velocityLabel = "Velocity (km/h)"
accelerationLabel = "Acceleration (m/s^2)"
positionLabel = "Position (km)"
entranceDistanceLabel = "Distance from entrance (km)"
enteredVehicleLabel = "Number of entered vehicles"
inflowRampEnteredTitle = "upstream Boundary"
inflowRoadEnteredTitle = "onramp Boundary"
inflowRampFlowTitle = "Q(t) in veh/h"

# Deutsch
### TODO - final 4 strings need translation
if (lang eq "de") fundTitle =  "Fundamentaldiagram Q({/Symbol r})";\
  veqTitle = "Geschwindigkeit V_{eq} (km/h)";\
  vqTitle = "Gleichgewichts-Geschwindigkeit V_{eq}({/Symbol r})";\
  densityLabel = "Dichte {/Symbol r} (1/km)";\
  flowLabel = "Fluss Q (1/h)";\
  veqLabel = "Geschwindigkeit V_{eq} (km/h)";\
  timeLabel = "Zeit (s)";\
  timeMinuteLabel = "Zeit (min)";\
  distanceLabel = "Abstand (m)";\
  velocityLabel = "Geschwindigkeit (km/h)";\
  accelerationLabel = "Beschleunigung (m/s^2)";\
  positionLabel = "Ort (km)";\
  entranceDistanceLabel = "Abstand von Zufahrt (km)";\
  enteredVehicleLabel = "Number of entered vehicles";\
  inflowRampEnteredTitle = "upstream Boundary";\
  inflowRoadEnteredTitle = "onramp Boundary";\
  inflowRampFlowTitle = "Q(t) in veh/h"

# line styles
set style line 1 linetype 1 linewidth 7 pointtype 7 pointsize 1.5 lc rgb "#000000"
set style line 2 linetype 7 linewidth 3 pointtype 5 pointsize 1.5 lc rgb "#dd0000"
set style line 3 linetype 1 linewidth 7 pointtype 9 pointsize 1.5 lc rgb "#ff8800"
set style line 4 linetype 3 linewidth 3 pointtype 11 pointsize 1.5 lc rgb "#00aa44"
set style line 5 linetype 5 linewidth 7 pointtype 13 pointsize 1.5 lc rgb "#220099"

min(x,y)=(x<y) ? x : y

set term post eps enhanced color dash "Helvetica" 26
set nogrid

########################################################

########################################################

set xlabel densityLabel

set title ldm." ".fundTitle
set ylabel flowLabel
set out fundOutput
plot fundFile using 1:4 title "" with lines linestyle 10

set title ldm." ".veqTitle
set ylabel veqLabel
set out veqOutput
plot fundFile using 1:3 title "" with lines linestyle 10

set title ldm." ".vqTitle
set xlabel flowLabel
set out vqOutput
plot fundFile u 4:3 title "" with lines linestyle 10

########################################################

set size 1,1
set title ""
set key box

########################################################

set xlabel densityLabel
set ylabel flowLabel

set xrange [0:150]
set out detOutput
plot\
 detFile(8000)  u 3:4 t "-7 km" w p ls 6,\
 detFile(10000) u 3:4 t "-5 km" w p ls 4,\
 detFile(12000) u 3:4 t "-3 km" w p ls 2,\
 detFile(14000) u 3:4 t "-1 km" w p ls 1,\
 fundFile u 1:4 t "FD" w l ls 10

set out detmicroOutput
plot\
 detFile(8000)  u ($8*3600/$7):($8*3600) t "-7 km" w p ls 6,\
 detFile(10000) u ($8*3600/$7):($8*3600) t "-5 km" w p ls 4,\
 detFile(12000) u ($8*3600/$7):($8*3600) t "-3 km" w p ls 2,\
 detFile(14000) u ($8*3600/$7):($8*3600) t "-1 km" w p ls 1,\
 fundFile u 1:4 t "FD" w l ls 10

######################################
# logging of inflows for testing
######################################

set auto x
set xlabel timeMinuteLabel
set ylabel enteredVehicleLabel
set out inflowOutput
plot\
 roadSourceFile  u ($1/60.):2 t inflowRampEnteredTitle w l ls 1,\
 rampSourceFile  u ($1/60.):2 t inflowRoadEnteredTitle w l ls 3,\
 roadSourceFile  u ($1/60.):7 t inflowRampFlowTitle w l ls 2

############### 3d ###########

set palette defined (0 "red", 20 "orange", 40 "yellow", 60 "green", 80 "blue",  100 "#dd00ff" )
set cbrange [0:140]
set pm3d; set pm3d map
#set contour surface
set cntrparam bspline
set cntrparam levels 15
unset clabel  # dann lauter gleiche Kontourlinien;
                     # Farbe und Typ mit "w l ls" beim splot-Kommando

#set view 20,63
#set hidden3d
set ticslevel 0
set nogrid

##################################

set nokey

set size 1.3,1.2
set label 1 "V (km/h)" at screen 1.1,1.02

xmin=-8  # km
xmax=2
xramp=15.  # km
tmin=0
tmax=90 # min
tmax=120
#tshift=20.

set term post eps enhanced color solid "Helvetica" 30

set xlabel timeMinuteLabel
set xrange [tmin:tmax]
set xtics 30

set ylabel entranceDistanceLabel offset -1.5,0
set yrange [xmin:xmax]

unset surface
set out v2dOutput
splot datFile u ($2*60):($1-xramp):4 w l ls 99

####

set auto x
set auto y
set ylabel positionLabel offset -1.0,0
set out v2dallOutput
splot datFile u ($2*60):1:4 w l ls 99

set nolabel
unset colorbox
unset pm3d
