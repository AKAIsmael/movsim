
set style line 99 lt 7 lw 1 linecolor rgb "#000000" #black

#################################
# Definition of Contour lines   (line style=line style indicated at splot)
#################################

unset contour              # no contour lines
set contour surface     
set cntrparam bspline 
unset clabel 
unset key
unset grid

#################################
# Definition of used color-coding
#################################

set pm3d; set pm3d map # gnuplot bug; set pm3d  map alone

set palette defined ( 0 "#dd00ff",  13 "#9933ff", 20 "#6666ff", 30 "#00eeee",\
      40 "green", 45 "#aaff00", 60 "yellow", 80 "orange", 100 "red") 

#########################################

set term post eps enhanced color solid "Helvetica" 20

set xlabel "V [km/h]"
set xrange [10:140]

set ylabel "acc [m/s^{2}]"
set yrange [-1:1]

set title "C100 [liters/100 km] for diesel vehicle"
set zrange [0:20]
unset ztics 

set cntrparam levels discrete 2, 4, 6, 8, 10 # freely set lines

set out "consumption100kmDiesel.eps"
splot  "templateConsumptionDiesel.jante_carConsumption.csv" u 1:2:6  w pm3d

####

set title "C100 [liters/100 km] for gasoline vehicle"

set out "consumption100kmGasoline.eps"
splot  "templateConsumptionGasoline.jante_carConsumption.csv" u 1:2:6  w pm3d


#########################################
set palette defined ( 0 "#dd00ff",  5 "#9933ff", 12 "#6666ff", 20 "#00eeee",\
      25 "green", 30 "#aaff00", 40 "yellow", 70 "orange", 100 "red") 

set xlabel "Frequency [rpm]"
set xrange [800:4500]

set ylabel "Torque [Nm]"
set yrange [0:230]

set title "Specific consumption(f,pressure) [g/kWh] for diesel vehicle"
set zrange [200:500]
unset ztics 

set cntrparam levels discrete 200,210,220,230,240,250,260,270,280,290,300 # freely set lines

set out "specConsumptionPressureDiesel.eps"
splot  "templateConsumptionDiesel.specCons_carConsumption.csv" u 1:4:5  w pm3d

####

set title "specific consumption(f,pressure) [g/kWh] for gasoline vehicle"
set xrange [900:6000]

set out "specConsumptionPressureGasoline.eps"
splot  "templateConsumptionGasoline.specCons_carConsumption.csv" u 1:4:5  w pm3d

#########################################

set title "specific consumption (f,power) [g/kWh] for diesel vehicle"
set xrange [800:4500]
set ylabel "Power [kW]"
set yrange [0:95]

set out "specConsumptionPowerDiesel.eps"
splot  "templateConsumptionDiesel.specCons_carConsumption.csv" u 1:2:5  w pm3d

### 

set title "specific consumption (f,power) [g/kWh] for gasoline Benzin vehicle"
set xrange [900:6000]

set out "specConsumptionPowerGasoline.eps"
splot  "templateConsumptionGasoline.specCons_carConsumption.csv" u 1:2:5  w pm3d
 
