# gnuplot command file

# parameterised startStop command file
# invoke using -e option to set parameters, eg:
# gnuplot -e "ldm='IDM'" startStop.gpl

set datafile separator ","

# set the output type
set terminal postscript eps enhanced color solid font "Helvetica" 26

# parameters
if (!exists("inputPath")) inputPath = ""
if (!exists("outputPath")) outputPath = "figs/"
if (!exists("ldm")) ldm = "IDM"
if (!exists("lang")) lang = "en"
if (!exists("compare")) compare = "false"
#print "LDM=".ldm
#print "lang=".lang

# variables
baseFile = "startStop_".ldm
vehicle = ldm."1"
c0 = 0
if (ldm eq "BARL") c0 = 1
if (ldm eq "GIPPS") c0 = 1
if (ldm eq "KCA") c0 = 1
if (ldm eq "NSM") c0 = 1

# input files
fundFile = inputPath.baseFile.".fund_".vehicle.".csv"
if (ldm eq "IDM_w_noise") fundFile = inputPath.baseFile.".fund_IDM1.csv"
carFile(n) = sprintf(inputPath.baseFile.".V%06d.csv",c0+n)
carLsaFile(n) = sprintf(inputPath.baseFile."LSA.car%d.csv",c0+n)

# output files
fundOutput = outputPath.baseFile.".fund_".vehicle.".eps"
veqOutput = outputPath.baseFile.".veq_".vehicle.".eps"
vqOutput = outputPath.baseFile.".vq_".vehicle.".eps"
distanceOutput = outputPath.baseFile.".s.eps"
velocityOutput = outputPath.baseFile.".v.eps"
accelerationOutput = outputPath.baseFile.".a.eps"
compareDistanceOutput = outputPath.baseFile."_compare.s.eps"
compareVelocityOutput = outputPath.baseFile."_compare.v.eps"
compareAccelerationOutput = outputPath.baseFile."_compare.a.eps"


########  Languages  #####################################

# English
fundTitle =  "Fundamental diagram Q({/Symbol r})"
veqTitle = "Equilibrium velocity V_{eq}({/Symbol r})"
vqTitle = "V-Q equilibrium relation V_{eq}(Q)"
densityLabel = "Density {/Symbol r} (vehicles/km)"
flowLabel = "Flow Q (vehicles/h)"
veqLabel = "Velocity V_{eq} (km/h)"
timeLabel = "Time (s)"
distanceLabel = "Separation (m)"
velocityLabel = "Velocity (km/h)"
accelerationLabel = "Acceleration (m/s^2)"
car(n) = sprintf("Car %d",n)

# Deutsch
if (lang eq "de") fundTitle =  "Fundamentaldiagram Q({/Symbol r})";\
  veqTitle = "Geschwindigkeit V_{eq} (km/h)";\
  vqTitle = "Gleichgewichts-Geschwindigkeit V_{eq}({/Symbol r})";\
  densityLabel = "Dichte {/Symbol r} (1/km)";\
  flowLabel = "Fluss Q (1/h)";\
  veqLabel = "Geschwindigkeit V_{eq} (km/h)";\
  timeLabel = "Zeit (s)";\
  distanceLabel = "Abstand (m)";\
  velocityLabel = "Geschwindigkeit (km/h)";\
  accelerationLabel = "Beschleunigung (m/s^2)";\
  car(n) = sprintf("Kfz %d",n)


########  Line Styles  ###################################

set style line 1 linetype 1 linewidth 4 pointtype 7 pointsize 1.5
set style line 2 linetype 2 linewidth 4 pointtype 5 pointsize 1.5
set style line 3 linetype 3 linewidth 4 pointtype 9 pointsize 1.5
set style line 4 linetype 4 linewidth 4 pointtype 11 pointsize 1.5
set style line 5 linetype 7 linewidth 4 pointtype 13 pointsize 1.5

set style line 10 linetype 1 linewidth 7 pointtype 7 pointsize 1.5

set style line 11 linetype 1 linewidth 6 pointtype 7 pointsize 1.5
set style line 12 linetype 7 linewidth 3 pointtype 5 pointsize 1.5
set style line 13 linetype 3 linewidth 6 pointtype 7 pointsize 1.5

set nogrid


#######  Scaling  ########################################

xscale=1.0
if (ldm eq "KCA") xscale = 0.5
if (ldm eq "BARL") xscale = 7.5
if (ldm eq "NSM") xscale = 7.5
tscale=1.0
vscale=xscale/tscale
rhoscale=1.0/xscale
ascale=xscale/(tscale*tscale)


########  Plot Fundamental Diagrams  #####################

set xlabel densityLabel

set title ldm." ".fundTitle
set ylabel flowLabel
set out fundOutput
plot fundFile using 1:4 title "" with lines linestyle 10

set title ldm." ".veqTitle
set ylabel veqLabel
set out veqOutput
plot fundFile using 1:3 title "" with lines linestyle 10

set title ldm." ".vqTitle
set xlabel flowLabel
set out vqOutput
plot fundFile u 4:3 title "" with lines linestyle 10

##########################################################

# common setup for plots
set title ldm

tmin=0
tmax=158
if (ldm eq "BARL") tmax=120
if (ldm eq "KCA") tmax=120
if (ldm eq "NSM") tmax=120
##tmaxdata=101.  # Um bei Beschl. Kurve zu stoppen

set xrange [tmin:tmax]
set xtics 30
set xlabel timeLabel

set size 1,0.75
#set key center right
set key right


########  Distance (separation) Plot  ####################

set ylabel distanceLabel
set yrange [0:35]
if (ldm eq "BARL") set yrange [0:55]
if (ldm eq "KCA") set yrange [0:55]
if (ldm eq "NSM") set yrange [0:55]
set ytics 10
# car 1: Entfernung zur Ampel !
# car 1 is lead car, so take distance to traffic light (col 8), rather than distance to car in front
set out distanceOutput
plot\
  carFile(1)  using 1:(xscale*$9) title car(1)  with lines linestyle 1,\
  carFile(5)  using 1:(xscale*$7) title car(5)  with lines linestyle 2,\
  carFile(10) using 1:(xscale*$7) title car(10) with lines linestyle 3,\
  carFile(15) using 1:(xscale*$7) title car(15) with lines linestyle 4,\
  carFile(20) using 1:(xscale*$7) title car(20) with lines linestyle 5

if (compare eq "true")\
  set out compareDistanceOutput;\
  plot\
    carFile(1)    using 1:(xscale*$8) title "java sim, ".car(1) with lines linestyle 11,\
    carLsaFile(1) using 1:(xscale*$5) title "mic sim,  ".car(1) with lines linestyle 12


########  Velocity Plot  #################################

set ylabel velocityLabel
set yrange [0:55]
set out velocityOutput
plot\
  carFile(1)  using 1:(3.6*vscale*$4) title car(1)  with lines linestyle 1,\
  carFile(5)  using 1:(3.6*vscale*$4) title car(5)  with lines linestyle 2,\
  carFile(10) using 1:(3.6*vscale*$4) title car(10) with lines linestyle 3,\
  carFile(15) using 1:(3.6*vscale*$4) title car(15) with lines linestyle 4,\
  carFile(20) using 1:(3.6*vscale*$4) title car(20) with lines linestyle 5

if (compare eq "true")\
  set out compareVelocityOutput;\
  plot\
    carFile(1)    using 1:(3.6*vscale*$4) title "java sim, ".car(1) with lines linestyle 11,\
    carLsaFile(1) using 1:(3.6*vscale*$4) title "mic sim,  ".car(1) with lines linestyle 12


########  Acceleration Plot  #############################

set ylabel accelerationLabel
set auto y
set ytics 1
if (ldm eq "BARL") set ytics 5
if (ldm eq "GIPPS") set yrange [-1.5:2]
if (ldm eq "KCA") set ytics 2; set yrange [-14:2]
if (ldm eq "NSM") set ytics 5
if (ldm eq "OVM") set ytics 5
set out accelerationOutput
plot\
  carFile(1)  using 1:(ascale*$5) title car(1)  with lines linestyle 1,\
  carFile(5)  using 1:(ascale*$5) title car(5)  with lines linestyle 2,\
  carFile(10) using 1:(ascale*$5) title car(10) with lines linestyle 3,\
  carFile(15) using 1:(ascale*$5) title car(15) with lines linestyle 4,\
  carFile(20) using 1:(ascale*$5) title car(20) with lines linestyle 5

if (compare eq "true")\
  set out compareAccelerationOutput;\
  plot\
    carFile(1)     using 1:(ascale*$5) title "java sim, ".car(1)  with lines linestyle 11,\
    carLsaFile(1)  using 1:(ascale*$5) title "mic sim,  ".car(1)  with lines linestyle 12,\
    carFile(10)    using 1:(ascale*$5) title "java sim, ".car(10) with lines linestyle 11,\
    carLsaFile(10) using 1:(ascale*$5) title "mic sim,  ".car(10) with lines linestyle 12
