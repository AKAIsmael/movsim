# gnuplot command file

# parameterised startStop command file
# invoke using -e option to set lang parameter, eg:
# gnuplot -e "lang='de'" test_speedlimits.gpl

if (!exists("lang")) lang = "en"
#print "lang=".lang

# variables
baseFile = "test_speedlimits"

# input files
carFile(n) = sprintf(baseFile.".%06d.car",n)

# output files
trajectoryOutput = "figs/".baseFile.".trajectories.eps"

# languages

# English
positionLabel = "Position (km)"
speedLabel = "Speed (km/h)"
car(n) = sprintf("Car %d",n)

# Deutsch
if (lang eq "de")\
  positionLabel = "Ort (km)";\
  speedLabel = "Geschwindigkeit (km/h)";\
  car(n) = sprintf("Kfz %d",n)

# line styles
set style line 1 linetype 7 linewidth 3 pointtype 7 pointsize 1.5

set term post eps enhanced color solid "Helvetica" 26

############################################################

set size 1,1

set xlabel positionLabel
set ylabel speedLabel
unset key
set pm3d map

set out trajectoryOutput
splot\
  carFile(1) using ($2/1000):(3.6*$3):(1) title car(1) with lines linestyle 1

