
set style line 1 lt 1 lw 7 pt 7 ps 1.5 lc rgb "#000000"
set style line 2 lt 7 lw 3 pt 5 ps 1.5 lc rgb "#dd0000"
set style line 3 lt 1 lw 7 pt 9 ps 1.5 lc rgb "#ff8800"
set style line 4 lt 3 lw 3 pt 11 ps 1.5 lc rgb "#00aa44"
set style line 5 lt 5 lw 7 pt 13 ps 1.5 lc rgb "#220099"

min(x,y)=(x<y) ? x : y

set term post eps enhanced color dash "Helvetica" 26
set nogrid

########################################################

########################################################
set xlabel "Dichte {/Symbol r} (1/km)"

set title "IDM Fundamentaldiagram Q({/Symbol r})"
set ylabel "Fluss Q (1/h)"
set out "./figs/onramp_IDM.fund_IDM1.eps"
plot "onramp_IDM.fund_IDM1" u 1:4 t "" w l ls 10

set title "IDM Gleichgewichts-Geschwindigkeit V_{eq}({/Symbol r})"
set ylabel "Geschwindigkeit V_{eq} (km/h)"
set out "./figs/onramp_IDM.veq_IDM1.eps"
plot "onramp_IDM.fund_IDM1" u 1:3 t "" w l ls 10

set title "IDM V-Q-Gleichgewichtsrelation V_{eq}(Q)"
set xlabel "Fluss Q (1/h)"
set out "./figs/onramp_IDM.vq_IDM1.eps"
plot "onramp_IDM.fund_IDM1" u 4:3 t "" w l ls 10

########################################################

set size 1,1
set title ""
set key box

########################################################
set xlabel "Dichte {/Symbol r} (1/km)"
set ylabel "Fluss Q (1/h)"

set xrange [0:150]
set out "./figs/onramp_IDM.det_q.eps"
plot\
 "onramp_IDM.x8000_det"  u 3:4 t "-7 km" w p ls 6,\
 "onramp_IDM.x10000_det" u 3:4 t "-5 km" w p ls 4,\
 "onramp_IDM.x12000_det" u 3:4 t "-3 km" w p ls 2,\
 "onramp_IDM.x14000_det" u 3:4 t "-1 km" w p ls 1,\
 "onramp_IDM.fund_IDM1" u 1:4 t "FD" w l ls 10
 
set out "./figs/onramp_IDM.det_q_micro.eps"
plot\
 "onramp_IDM.x8000_det"  u ($8*3600/$7):($8*3600) t "-7 km" w p ls 6,\
 "onramp_IDM.x10000_det" u ($8*3600/$7):($8*3600) t "-5 km" w p ls 4,\
 "onramp_IDM.x12000_det" u ($8*3600/$7):($8*3600) t "-3 km" w p ls 2,\
 "onramp_IDM.x14000_det" u ($8*3600/$7):($8*3600) t "-1 km" w p ls 1,\
 "onramp_IDM.fund_IDM1" u 1:4 t "FD" w l ls 10
 
 
 
######################################
# logging of inflows for testing
######################################
set auto x
set xlabel "Time (min)"
set ylabel "Number of entered vehicles" 
set out "./figs/onramp_IDM.inflow_log.eps"
plot\
 "onramp_IDM.upBC_log"  u ($1/60.):2 t "upstream Boundary" w l ls 1,\
 "onramp_IDM.rmp1_log"  u ($1/60.):2 t "onramp Boundary" w l ls 3,\
 "onramp_IDM.upBC_log"  u ($1/60.):7 t "Q(t) in veh/h" w l ls 2
 
############### 3d ###########


set palette defined (0 "red", 20 "orange", 40 "yellow", 60 "green", 80 "blue",  100 "#dd00ff" ) 
set cbrange [0:140]
set pm3d; set pm3d map
#set contour surface
set cntrparam bspline 
set cntrparam levels 15
unset clabel  # dann lauter gleiche Kontourlinien; 
                     # Farbe und Typ mit "w l ls" beim splot-Kommando


#set view 20,63
#set hidden3d
set ticslevel 0
set nogrid


##################################

set nokey

set size 1.3,1.2
set label 1 "V (km/h)" at screen 1.1,1.02

xmin=-8  # km
xmax=2
xramp=15.  # km
tmin=0   
tmax=90 # min
tmax=120
#tshift=20.  

set term post eps enhanced color solid "Helvetica" 30
set xlabel "Zeit (min)"
set xrange [tmin:tmax]
set xtics 30

set ylabel "Abstand von Zufahrt (km)" offset -1.5,0

set yrange [xmin:xmax]

unset surface

set out "./figs/onramp_IDM.v2d.eps"
splot "onramp_IDM.dat" u ($2*60):($1-xramp):4 w l ls 99

####

set auto x
set auto y
set ylabel "Ort (km)" offset -1.0,0
set out "./figs/onramp_IDM.v2d_all.eps"
splot "onramp_IDM.dat" u ($2*60):1:4 w l ls 99


set nolabel
unset colorbox
unset pm3d

